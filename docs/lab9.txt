Guy Emerson, Travis Nguyen
Prof. Emily Menon Bender
LING 567
May 26, 2017

---------------
Lab #9 Write-Up
---------------

For this assignment, we have included a few changes in our grammar of Abui to
accommodate machine translation between the aforementioned language and English
and Chadian Arabic. Such changes include a few bug changes, new lexical
entries, and the inclusion of new transfer rules.

-----------------------------------
Preparation for machine translation
-----------------------------------

To prepare English and Chadian Arabic for machine translation, the following
line has been included in the lkb/globals.lsp file of both the English and
Chadian Arabic grammars:

(setf *translate-grid* '(:eng :shu :abz))

Likewise, the following line has been included in the lkb/globals.lsp file of
the Abui grammar:

(setf *translate-grid* '(:abz :eng :shu))

We have also included the files abz.txt, eng.txt, and shu.txt,
which list the MMT sentences for Abui, English, and Chadian Arabic, respectively.
We did not translate the sentences containing "ask" and "think".

---------
Bug fixes
---------

A few changes have been included to account for bugs in the previous lab work.

The dem-lex type now specifies a head value --
this is to prevent the person-marking prefix rules from applying,
which led to a spinning rule (pos-opt-subj) in generation.

dem-lex now also inherits from no-rels-hcons-icons-lex-item rather than lex-item --
this is to make sure that the difference lists are empty,
so that the sentence-level lists are fully specified,
which fixes a problem in generation.

We also moved the DEM-COG-ST and ADDRS-VIEWPOINT features
to be inside INFLECTED, rather than at the root level.

raw-dem := inflected &
  [ DEM-COG-ST cog-st,
    ADDRS-VIEWPOINT bool ].

dem-lex := no-rels-hcons-icons-lex-item &
  [ SYNSEM.LOCAL.CAT.HEAD +rd,
    INFLECTED raw-dem ].

Our analysis of N-coordination, taken from the Matrix,
does not introduce enough quantifiers,
which means that the MRSs do not match the eng/shu outputs.
As a quick and dirty fix,
we switched from N-coordination to NP-coordination,
which introduces more quantifiers.
We also added a constraint to say that coordinated NPs are 3rd person.
(It's not clear how the agent markers interact with coordination.)

n1-top-coord-rule := basic-np-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL [ COORD-STRAT "1",
                   CONT.HOOK.INDEX.PNG.PERNUM 3rd ] ].

Switching to NP-coordination did however expose a bug in
the rule for predicative NPs, which could apply to half a coordinate phrase.
It would seem reasonable for all non-coordiation rules
to inherit from a type that blocks this.
As a quick fix, we added the following constraint to np-predicate-rule:
  ARGS.FIRST.SYNSEM.LOCAL.COORD -

Possessive constructions were not working correctly in wh-clauses.
This was due to the non-local values not being passed up,
and was fixed by pos-lex-rule inheriting from same-non-local-lex-rule.

----------------------------------------------------------
Re-analysis of agent markers as case-marking postpositions
----------------------------------------------------------

We initially analysed agent markers as pronouns,
but we have since realised that these markers can co-occur with an NP.
(At least for the 3rd person agent marker)
As it appears that an agent marker cannot be separated from the NP,
we have analysed them as case-marking postpositions.

We added a CASE feature, and specified that nouns are 3rd person:

case := *top*.
agentive := case.
non-agentive := case.

ref-ind :+
  [ CASE case ].

noun-lex := basic-noun-lex & basic-one-arg & no-hcons-lex-item & non-mod-lex-item &
  [ SYNSEM.LOCAL [ CAT [ VAL [ SPR < #spr &
                                     [ LOCAL.CAT.HEAD det ] >,
                               COMPS < >,
                               SUBJ < >,
                               SPEC < > ],
                         HEAD noun ],
                   CONT.HOOK.INDEX.PNG.PERNUM 3rd ],
    ARG-ST < #spr > ].

The agentive markers inherit from agent-lex,
which is semantically empty (it inherits from raise-sem-lex-item),
takes a single NP complement,
and specifies agentive case.
One of the six agent markers is given below.

agent-lex := raise-sem-lex-item & basic-one-arg & 
  [ ARG-ST.FIRST #comp & [ LOCAL.CAT [ HEAD noun,
                                       VAL [ COMPS < >,
                                             SPR < >,
                                             SUBJ < > ] ] ],
    SYNSEM.LOCAL [ CAT [ VAL [ COMPS < #comp >,
                               SPR < >,
                               SUBJ < >,
                               SPEC < > ],
                         HEAD adp & [ MOD < > ] ],
                   CONT.HOOK.INDEX.CASE agentive ] ].

1sg-agent-lex := agent-lex &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.PERNUM 1sg ].

As the agent markers do not require a co-occurring NP,
we have also added a rule that discharges the complement.
It is identical to basic-head-opt-comp-phrase, but with ref-ind, instead of an event.

agent-opt-np-phrase := head-valence-phrase & head-only & head-compositional &
  [ INFLECTED #infl,
    SYNSEM canonical-synsem &
              [ LOCAL.CAT [ VAL [ SUBJ #subj,
                                  COMPS #comps,
                                  SPR #spr,
                                  SPEC #spec ],
                            MC #mc,
                            POSTHEAD #ph ],
                MODIFIED #mod ],
    HEAD-DTR [ INFLECTED #infl & infl-satisfied,
               SYNSEM [ LOCAL [ CAT [ VAL [ SUBJ #subj,
                                            COMPS < unexpressed &
                                                    [ OPT +,
                                                      OPT-CS #def,
                                                      LOCAL.CONT.HOOK [ INDEX #index & [ COG-ST #def ],
                                                                        ICONS-KEY #ikey,
                                                                        CLAUSE-KEY #ckey ] ] . #comps >,
                                            SPR #spr,
                                            SPEC #spec ],
                                      MC #mc,
                                      POSTHEAD #ph ],
                                CONT.HOOK.INDEX ref-ind ],
                        MODIFIED #mod ] ],
    C-CONT [ RELS <! !>,
             HCONS <! !>,
             ICONS <! #ikey & non-focus & [ IARG1 #ckey,
                                            IARG2 #index ] !> ] ].

Finally, verbs now specify agentive case on the subject,
and specify it must be a noun or adposition.
This is not true for all verb classes, but this is beyond the scope of this lab.
(We also refactored the verb types to reduce redundancy.)

verb-lex := basic-verb-lex & non-mod-lex-item &
  [ ARG-ST < #subj & [ LOCAL [ CAT [ VAL [ SPR < >,
                                           COMPS < > ],
                                     HEAD +np ],
                               CONT.HOOK.INDEX #xarg & [ CASE agentive ] ] ]
             . #comps >,
    SYNSEM.LOCAL [ CONT.HOOK.XARG #xarg,
                   CAT [ VAL [ SPR < >,
                               SPEC < >,
                               SUBJ < #subj >,
                               COMPS #comps ],
                         HC-LIGHT - ] ] ].

Verbal objects and subjects of predicative adjectives did not need to be changed --
as they must combine with NPs, rather than PPs,
they cannot combine with the case-marked phrases.
So, in fact, the CASE feature may not be necessary,
as we could just use the head type alone.

-------------------
New lexical entries
-------------------

To account for the lexical gaps in Abui that we needed to cover to perform
machine translation using the test sentences in English and Chadian Arabic,
we have included a few new lexical entries.

The following entries are examples of what we have included in the lexicon.tdl
file:

kamai := alienable-noun-lex &
  [ STEM < "kamai" >,
    SYNSEM.LKEYS.KEYREL.PRED "_cat_n_rel" ].

loi := trans-verb-lex &
  [ STEM < "loi" >,
    SYNSEM.LKEYS.KEYREL.PRED "_chase_v_rel" ].

marai := adj-adj-lex &
  [ STEM < "marai" >,
    SYNSEM.LKEYS.KEYREL.PRED "_hungry_a_rel" ].

For a few cases, some words could not be found in our Abui resource grammar.
As a result, we have performed a few substitutions, such as using the Abui
word for field/garden (`ut`) in place of "park".

We have chosen the part of speech of the predicate
to match the English predicate name.
For the verb `mi`, we have used _in_p_rel, rather than _in_v_rel.

----------------------------
Changes to the semi.vpm file
----------------------------

We introduce a no-cog-st type, similarly to no-aspect,
and mapped fully underspecified cognitive status to this type,
to reduce generation outputs.

no-cog-st := cog-st.

COG-ST : COG-ST
  type-id <> type-id
  uniq-id <> uniq-id
  familiar <> familiar
  activated <> activated
  in-foc <> in-foc
  activ+fam <> activ+fam
  uniq+fam <> uniq+fam
  activ-or-more <> activ-or-more
  uniq-or-less <> uniq-or-less
  uniq+fam+act <> uniq+fam+act
  fam-or-more <> fam-or-more
  fam-or-less <> fam-or-less
  uniq-or-more <> uniq-or-more
  activ-or-less <> activ-or-less
  no-cog-st << *
  no-cog-st << [x]
  no-cog-st << [e]

We have also split pernum into two features.
As English and Chadian Arabic do not have an inclusive/exclusive distinction,
we have dropped this in the output.

PNG.PERNUM : PER NUM
  3rd <> third !
  3rd << third *
  2pl <> second pl
  1pl_excl >> first pl
  1pl_incl >> first pl
  1pl << first pl
  2sg <> second sg
  1sg <> first sg
  2nd <> second !
  2nd << second *
  1st <> first !
  1st << first *
  * >> ! !
  ! << * *

We removed MOOD and TENSE mappings, as we aren't using these,
as we changed
E.ASPECT : E.ASPECT
to
E.ASPECT : ASPECT

---------------
Object prefixes
---------------

Abui has three series of object prefixes documented in the grammar
(and an additional two documented elsewhere),
but we have not implemented an analysis which distinguishes them.
Abui also uses different prefixes in the third person,
to indicate coreference with the agent, and distributive readings.
Currently, all of the third person prefixes are treated identically
(although we have implemented the distinction in their use as possessive markers).
To prevent a large number of outputs,
we have simply commented out the prefixes not necessary for the MMT sentences.

-------------------
Machine translation
-------------------

For both of the English and Chadian Arabic grammars,
we included the pro-drop rule in acm.mtr:

pro-drop := pronoun-delete-mtr.

We made no other changes.

Apart from the sentences with embedded questions,
which we did not translate into Abui,
a few phenomena are not currently covered by our grammar:

(1) Adjectives being used as adverbs

The grammar gives a small number of adverbs as a closed class,
which we previously implemented.
However, when searching for translations of "quickly",
it appears that an adjective meaning "quick" can also be used as an adverb,
without any additional marking:

di aleka firai
3  quick runs
"He runs fast"

Semantically, it appears that `aleka` is modifying `firai`,
rather than being coordinated with it.

It seems likely this construction would apply to other adjectives,
but this is not documented in the grammar,
and we have not had time to implement it.

(2) S-level coordination

Our grammar currently models VP-coordination but not S-coordination.
We have not had time to add an analysis of S-coordination.

(3) Durative suffix for translating "are in"

Our translation of "are in" includes the durative suffix.
However, since we are suppressing aspect unless it is in the input,
this does not appear in the output of translation.
We have not had time to implement a transfer rule
that would add durative aspect for _in_p_rel.

---------
Testsuite
---------

                Coverage       Overgeneration
Lab 8 baseline   90.1%              45.3%
Lab 9            77.8%              18.9%

The drop in coverage is due to the commenting out of lexical items.
If we don't comment them out, this becomes:

Lab 9 uncomment  87.7%              24.5%

The drop in coverage and overgeneration are both due to
the new analysis of agent markers,
which can no longer be coordinated or act as predicative NPs.
For the two grammatical sentences that are no longer parsed,
neither was being given a correct analysis.

Guy Emerson, Travis Nguyen
Prof. Emily Menon Bender
LING 567
19 May 2017

---------------
Lab #8 Write-Up
---------------

This write-up documents the work done for Lab #8. It includes working on
phenomena found in one sentence from the test corpus as well as working on
translating the sentence "Dogs sleep" from English to Abui via machine
translation, including a fix for variable property mapping for unspecified
aspect.

---------------------------------
One sentence from the test corpus
---------------------------------

We have finished implementing an analysis for the test sentence from last time:

#127 Grammatical (one sentence from the test corpus)
Source: a:422
Vetted: t
Judgment: g
Phenomena: {corpus}
e-sora e-sapada do lakang masena do
2SG.AL-sword 2SG.AL-machete PRX really be.nice PRX
'your swords and machetes are really nice!'

We first cut down on the spurious ambiguity of possessed nouns,
so that determiners must attach after possessors.
This was done by requiring an empty SUBJ list.
We also removed overgeneration from an adjective
acting first predicatively and then attributively.
This was done using the PRD feature.

head-adj-int-phrase :+
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.PRD - ].

n-coord-phrase :+
  [ LCOORD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < >,
    RCOORD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < > ].

head-spec-phrase :+
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < > ].

subj-head-phrase :+
  [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < >,
    HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.PRD + ].

To analyse the clause-final "anaphoric demonstrative",
we first split the analysis of determiners into a lexical item and a lexical rule.
We also needed to relax the COG-ST of the proximal determiners,
from in-foc to activ-or-more,
so that it can unify with the uniq+fam+activ of a possessed noun

raw := inflected.

dem-lex := lex-item &
  [ DEM-COG-ST cog-st,
    ADDRS-VIEWPOINT bool,
    INFLECTED raw ].

speak-dem-lex := dem-lex &
  [ ADDRS-VIEWPOINT - ].
addrs-dem-lex := dem-lex &
  [ ADDRS-VIEWPOINT + ].

prx-dem-lex := dem-lex &
  [ DEM-COG-ST activ-or-more ].
med-dem-lex := dem-lex &
  [ DEM-COG-ST activ+fam ].

prx-sp-dem-lex := prx-dem-lex & speak-dem-lex.
prx-ad-dem-lex := prx-dem-lex & addrs-dem-lex.
med-sp-dem-lex := med-dem-lex & speak-dem-lex.
med-ad-dem-lex := med-dem-lex & addrs-dem-lex.

uniq-id-dem-lex := dem-lex &
  [ DEM-COG-ST uniq-id ].
type-id-dem-lex := dem-lex &
  [ DEM-COG-ST type-id ].

determiner-lex-rule := basic-determiner-lex & norm-zero-arg & non-mod-lex-item & no-icons-lex-item & const-lex-rule &
  [ SYNSEM [ LKEYS.KEYREL.PRED "exist_q_rel",
	     LOCAL [ CAT.VAL [ SPR < >,
			       COMPS < >,
			       SUBJ < > ],
		     CONT.HOOK.INDEX [ SPECI +,
				       COG-ST #cog-st ] ] ],
    DTR dem-lex & [ DEM-COG-ST #cog-st ] ].

We then wrote a second lexical rule,
which converts a dem-lex item to a clause-final adverb:

clause-final-dem-lex-rule := no-rels-hcons-icons-lex-item & const-lex-rule &
  [ SYNSEM [ LOCAL.CAT [ HEAD adv &
			      [ MOD < [ LOCAL intersective-mod &
					      [ CONT.HOOK.INDEX.COG-ST #cog-st,
						CAT [ HEAD +vj,
						      VAL [ SUBJ < >,
							    COMPS < > ] ] ],
					MODIFIED notmod ] > ],
			 VAL [ SPR < >,
			       COMPS < >,
			       SUBJ < >,
			       SPEC < > ],
			 POSTHEAD + ] ],
    DTR dem-lex & [ DEM-COG-ST #cog-st ] ].

As this rule has no RELS, the MODIFIED feature is used to prevent it recursing.
As with the determiners, this adds COG-ST information to the INDEX.

With the above changes, we can parse the corpus sentence,
assigning it a single reading.


In addition, we have looked at a second sentence:

#35 snippet of example (35) from Abui grammar
Source: a:431
Vetted: t
Judgment: g
Phenomena: {}
pi ning ayoku yaa mi-a yo
1PL.I be.QNT two go take-DUR MD.AD
'(the two of us) let's go steal!'

There are a few phenomena that the aforementioned sentence features that were
not previously covered in our grammar:
(1) the imperative form
(2) quantifying verbs (i.e., `ning`) - note the SVO word order, rather than SOV.

Quantifying verbs in Abui quantify the number of participants. The author has
analyzed them as verbs because they can combine with pronomial prefixes.

To account for the two phenomena that we listed, we have included the
following changes in our grammar:

quant-verb-verb-lex := verb-lex & transitive-lex-item &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < #comps >,
    ARG-ST < [ LOCAL.CAT.HEAD noun ],
             #comps &
             [ LOCAL.CAT [ VAL [ SPR < >,
                                 COMPS < > ],
                           HEAD noun ] ] > ].


num-lex := basic-int-adverb-lex &
  [ SYNSEM.LOCAL.CAT [ POSTHEAD +,
                       HEAD.MOD < [ LOCAL.CAT.HEAD verb ] >,
                       VAL [ COMPS < >,
                             SUBJ < >,
                             SPR < >,
                             SPEC < > ] ] ].

This is a work in progress.

--------------------------------------
Variable Property Mapping and First MT
--------------------------------------

The first time that we tried to translate the sentence "Dogs sleep" from
English to Abui, the generator output was unconstrained for aspect. As a
result, the generator produced the correct translation `kaai taa' (lit. "dog
sleep") as well as translations that included every aspect marker on the verb
`taa` (e.g., `kaai taa-i`, `kaai taa-o`, etc.).

In order to remedy this, we added two changes: one to abui.tdl and the other
to semi.vpm.

In abui.tdl, we included the following line to declare a "no-aspect" aspect:

no-aspect := aspect.

In semi.vpm, we replaced the following line under "E.ASPECT : E.ASPECT"

  * <> *

with the following line

  * >> no-aspect
  no-aspect << [e]

.

After saving the changes, reloading the grammar, and attempting to translate
the English sentence "Dogs sleep" again, the generator correctly produced
the only translation `kaai taa`.

In addition, the internal MRS of the translation `kaai taa` and the internal
MRS of the correct parse of `kaai taa` from the target LKB are the same,
indicating that semantics of the translation is correct.*

* We are getting overgeneration of the phrase `kaai taa` in the target LKB.
Namely, the readings of the two parses being produced by the target LKB are
listed below:

(1) The dogs sleep.
(2) It is a dog and is sleeping.

(1) is the expected parse. The reason for (2) is that Abui is a zero-copula
language and has argument drop. For the time being, we are disregarding this
because we have not been able to devise a solution for the issue.

--------
Coverage
--------

We have 90% coverage, improving on last week's 89%,
because we can now parse the corpus sentence.

Overgeneration is the same (45%).
